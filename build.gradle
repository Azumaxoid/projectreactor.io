ext {
  ratpackVersion = '0.9.15'
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

buildscript {
  repositories {
    maven { url 'http://repo.spring.io/libs-snapshot' }
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.1.RELEASE',
        'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
  }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'org.asciidoctor.convert'

group = 'io.projectreactor'
version = '0.4.0.BUILD-SNAPSHOT'

jar {
  baseName = 'projectreactor-home'
}

repositories {
  maven { url 'http://repo.spring.io/libs-snapshot' }
  maven { url 'http://oss.jfrog.org/artifactory/repo' }
  mavenCentral()
}

dependencies {
  // Spring Boot
  compile('org.springframework.boot:spring-boot-starter-web') {
    exclude module: 'spring-boot-starter-tomcat'
  }

  compile "io.projectreactor:reactor-core:2.0.1.BUILD-SNAPSHOT"

  compile "io.ratpack:ratpack-core:$ratpackVersion",
      "io.ratpack:ratpack-groovy:$ratpackVersion",
      "io.ratpack:ratpack-jackson:$ratpackVersion"
  compile 'org.springframework.boot:spring-boot-ratpack:1.0.0.BUILD-SNAPSHOT'

  runtime 'javax.el:javax.el-api:3.0.0'

  testCompile 'org.springframework.boot:spring-boot-starter-test'
}

asciidoctorj {
  version = '1.5.2'
}

asciidoctor {
  separateOutputDirs = false
  outputDir = sourceSets.main.resources.srcDirs + '/public/docs'

  attributes docinfo1: true,
      'source-highlighter': 'coderay',
      'hide-uri-scheme': true,
      sectanchors: true,
      sectlinks: true,
      icons: 'font'
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.3'
}
